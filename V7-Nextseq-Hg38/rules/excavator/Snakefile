excavator_version = 'EXCAVATOR2_Package_v1.1.2'

rule excavator_create_reference:
    # create a bed file with four fields
    input:
        bed="{prefix}%s{exometrack}.bed" % (config['dirs']['references'])
    output:
        bed="{prefix}%s%s/{exometrack}.bed" % (config['dirs']['references'], excavator_version)
    log:
        "{prefix}%s%s%s/create_reference/{exometrack}.log" % (config['dirs']['logs'], config['dirs']['references'], excavator_version)
    benchmark:
        "{prefix}%s%s%s/create_reference/{exometrack}.benchmark" % (config['dirs']['benchmarks'], config['dirs']['references'], excavator_version)
    threads:
        1
    shell:
        'cat {input.bed} | sed "s/$/\tdummy/" > {output.bed} 2> {log}'


rule excavator_install:
    input:
        sources="%s%sEXCAVATOR2_Package_v1.1.2.tgz" % (config['dirs']['prefix'], config['dirs']['references']),
        bed="{prefix}%s{exometrack}.bed" % (config['dirs']['references']),
        bigwig="{prefix}%s{assembly}.fa.index.gem.mappability.bw" % (config['dirs']['references']),
        fasta="{prefix}%s{assembly}.fa" % (config['dirs']['references']),
        mouse_coord="{prefix}%sChromosomeCoordinate_Mus_musculus.GRCm38.71.dna.txt" % (config['dirs']['references']),
        mouse_ucsd="{prefix}%sMus_musculus.GRCm38.71.dna.UCSC.txt" % (config['dirs']['references']),
        mouse_centromeres="{prefix}%sCentromerePosition_Mus_musculus.GRCm38.71.dna.txt" % (config['dirs']['references']),
    output:
        config="{prefix}%s%s/{assembly,hGRC37|Mus_musculus.GRCm38.71.dna}/{exometrack}/SourceTarget.txt" % (config['dirs']['intermediate'], config['stepnames']['excavator_install']),
        target=directory("{prefix}%s%s/%s/data/targets/{assembly,hGRC37|Mus_musculus.GRCm38.71.dna}/{exometrack}" % (config['dirs']['intermediate'], config['stepnames']['excavator_install'], excavator_version)),
    log:
        "{prefix}%s%s/{assembly}/{exometrack}.log" % (config['dirs']['logs'], config['stepnames']['excavator_install'])
    benchmark:
        "{prefix}%s%s/{assembly}/{exometrack}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['excavator_install'])
    conda:
        "envs/spike_excavator.yaml"
    threads:
        1
    params:
        windowsize="20000",
        installDir=lambda wildcards: '%s%s%s/%s' % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_install'], excavator_version)
    shell:
        "cwd=`pwd` 2> {log}"
        ' && module unload R 2>> {log}'
        " && if [ ! -e {params.installDir}/TargetPerla.pl ]; then"
        " mkdir -p -v {params.installDir} 2>> {log} 1>&2"
        " && tar xzvf {input.sources} -C {params.installDir}/../ 2>> {log} 1>&2"
        # we need to patch the Perl program to fix detection of working directory
        ' && sed -i "s#\$workingfolder=substr \$myscript, 0, (\$L1 - \$L2 - 1);#\$workingfolder=\\"{params.installDir}\\";#" {params.installDir}/TargetPerla.pl 2>>{log}'
        ' && sed -i "s#\$workingfolder=substr \$myscript, 0, (\$L1 - \$L2 - 1);#\$workingfolder=\\"{params.installDir}\\";#" {params.installDir}/EXCAVATORDataPrepare.pl 2>>{log}'
        ' && sed -i "s#\$workingfolder=substr \$myscript, 0, (\$L1 - \$L2 - 1);#\$workingfolder=\\"{params.installDir}\\";#" {params.installDir}/EXCAVATORDataAnalysis.pl 2>>{log}'
        ' && sed -i "s#\$workingfolder=substr \$myscript, 0, (\$L1 - \$L2 - 22);#\$workingfolder=\\"{params.installDir}\\";#" {params.installDir}/lib/perl/ReadPerla.pl 2>>{log}'
        ' && sed -i "s#\$workingfolder=substr \$myscript, 0, (\$L1 - \$L2 - 25);#\$workingfolder=\\"{params.installDir}\\";#" {params.installDir}/lib/perl/AnalyzePerla.pl 2>>{log}'

        # we also need to patch lib/R/FilterTarget.R to allow mouse assembly as input
        ' && head -n 22 {params.installDir}/lib/R/FilterTarget.R | sed "s/hg19/hGRC37/" > help 2>>{log}'
        ' && echo "if (assembly==\\\"Mus_musculus.GRCm38.71.dna\\\"){{\n  CoordIn <- paste(ProgramFolder,\\\"data/support\\\",assembly,\\\"ChromosomeCoordinate_Mus_musculus.GRCm38.71.dna.txt\\\",sep=\\\"/\\\")\n  FileGap <- paste(ProgramFolder,\\\"data/support\\\",assembly,\\\"Mus_musculus.GRCm38.71.dna.UCSC.txt\\\",sep=\\\"/\\\")\n}}" >> help 2>>{log}'
        ' && tail -n +22 {params.installDir}/lib/R/FilterTarget.R >> help 2>> {log}'
        ' && mv -v help {params.installDir}/lib/R/FilterTarget.R 2>>{log} 1>&2'
        # patch chromosome numbering for mouse
        ' && head -n 54 {params.installDir}/lib/R/FilterTarget.R > help 2>>{log}'
        ' && echo "if (assembly==\\\"Mus_musculus.GRCm38.71.dna\\\") {{\n  if (nchar(Chr[1])<3) {{\n    ChrVec<-c(1:19,\\\"X\\\")\n    ChrCoord<-c(1:19,\\\"X\\\")\n  }}\n  if (nchar(Chr[1])>3) {{\n    ChrVec<-paste(\\\"chr\\\",c(1:19,\\\"X\\\"),sep=\\\"\\\")\n    ChrCoord<-paste(\\\"chr\\\",c(1:19,\\\"X\\\"),sep="")\n  }}\n}}" >> help 2>>{log}'
        ' && tail -n +54 {params.installDir}/lib/R/FilterTarget.R >> help 2>> {log}'
        ' && mv -v help {params.installDir}/lib/R/FilterTarget.R 2>>{log} 1>&2'

        # and copy according files into correct directory
        ' && mkdir -p {params.installDir}/data/support/Mus_musculus.GRCm38.71.dna 2>>{log}'
        ' && cp -v {input.mouse_coord} {params.installDir}/data/support/Mus_musculus.GRCm38.71.dna/ 2>>{log} 1>&2'
        ' && cp -v {input.mouse_ucsd} {params.installDir}/data/support/Mus_musculus.GRCm38.71.dna/ 2>>{log} 1>&2'
        # and rename hg19 data directory
        ' && mv -v {params.installDir}/data/support/hg19 {params.installDir}/data/support/hGRC37 2>>{log} 1>&2'

        ' && cp -v {input.mouse_centromeres} {params.installDir}/data/centromere/ 2>>{log} 1>&2'
        ' && mv -v {params.installDir}/data/centromere/CentromerePosition_hg19.txt {params.installDir}/data/centromere/CentromerePosition_hGRC37.txt 2>>{log} 1>&2'
        " && cd {params.installDir}/lib/F77 2>> {log}"
        " && R CMD SHLIB F4R.f -L/usr/lib64/ -Wl,-rpath=/usr/lib64/ 2>> {log} 1>&2"
        " && R CMD SHLIB FastJointSLMLibraryI.f -L/usr/lib64/ -Wl,-rpath=/usr/lib64/ 2>> {log} 1>&2"
        " && cd $cwd"
        " ; fi"

        # create configuration file
        ' && echo "{input.bigwig} {input.fasta}" > {output.config}'

        # execute target initialization
        " && perl {params.installDir}/TargetPerla.pl"
        " {output.config}"
        " {input.bed}"
        " {wildcards.exometrack}"
        " {params.windowsize}"
        " {wildcards.assembly}"
        " 2>> {log} 1>&2"

        # some conda magic:
        # we will run into some concurrency issues when activating the same conda env at the same time multiple times (as done when processing several samples)
        # further details: https://github.com/conda-forge/r-base-feedstock/issues/67
        # I will try to workaround by deleting the offending activation script, after Excavator is installed
        " && rm -v -f $CONDA_PREFIX/etc/conda/activate.d/activate-r-base.sh 2>> {log}"


rule excavator_dataprepare:
    input:
        assembly=lambda wildcards: "%s%s%s.fa" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(wildcards.sample, SAMPLESHEETS, config)['file']),
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(wildcards.sample, SAMPLESHEETS, config)),
        sample="{prefix}%s%s/{sample}.reCal.reAl.nodup.srt.bam" % (config['dirs']['intermediate'], config['stepnames']['gatk_PrintReads']),
        target=lambda wildcards: directory("%s%s%s/%s/data/targets/%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_install'], excavator_version, get_reference_genome(wildcards.sample, SAMPLESHEETS, config)['file'].split('/')[-1], get_reference_exometrack(wildcards.sample, SAMPLESHEETS, config).split('/')[-1][:-4]))
    output:
        sample=directory("{prefix}%s%s/{sample}" % (config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'])),
    log:
        "{prefix}%s%s/{sample}.log" % (config['dirs']['logs'], config['stepnames']['excavator_dataprepare'])
    benchmark:
        "{prefix}%s%s/{sample}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['excavator_dataprepare'])
    conda:
        "envs/spike_excavator.yaml"
    threads:
        1
    shell:
        # see excavator_install for the following line
        " rm -v -f $CONDA_PREFIX/etc/conda/activate.d/activate-r-base.sh 2> {log}"
        # make sure we are using conda's R and not the one loaded by module
        ' && module unload R 2>> {log}'
        ' && assembly=`basename "{input.assembly}" | rev | cut -b 4- | rev` 2>> {log}'
        ' && exometrack=`basename "{input.exometrack}" | rev | cut -b 5- | rev` 2>> {log}'
        ' && configfile=$(mktemp) 2>> {log} 1>&2'
        ' && echo "`readlink -f {input.sample}` {output.sample} `basename {output.sample}`" > $configfile 2>> {log}'
        " && perl {wildcards.prefix}{config[dirs][intermediate]}{config[stepnames][excavator_install]}/{excavator_version}/EXCAVATORDataPrepare.pl"
        " $configfile"
        " --processors {threads}"
        " --target $exometrack"
        " --assembly $assembly"
        " 2>> {log} 1>&2"


rule excavator_somatic:
    input:
        assembly=lambda wildcards: "%s%s%s.fa" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'tumor', SAMPLESHEETS), SAMPLESHEETS, config)['file']),
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'tumor', SAMPLESHEETS), SAMPLESHEETS, config)),
        tumor=directory(lambda wildcards: "%s%s%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'], get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'tumor', SAMPLESHEETS))),
        healthy=directory(lambda wildcards: "%s%s%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'], get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'healthy', SAMPLESHEETS))),
    output:
        directory("{prefix}%s%s/{entity}" % (config['dirs']['intermediate'], config['stepnames']['excavator_somatic']))
    log:
        "{prefix}%s%s/{entity}.log" % (config['dirs']['logs'], config['stepnames']['excavator_somatic'])
    benchmark:
        "{prefix}%s%s/{entity}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['excavator_somatic'])
    conda:
        "envs/spike_excavator.yaml"
    threads:
        1
    params:
        name_tumor=lambda wildcards: wildcards.entity.split('/')[-1]
    shell:
        # see excavator_install for the following line
        " rm -v -f $CONDA_PREFIX/etc/conda/activate.d/activate-r-base.sh 2> {log}"
        # make sure we are using conda's R and not the one loaded by module
        ' && module unload R 2>> {log}'
        ' && assembly=`basename "{input.assembly}" | rev | cut -b 4- | rev` 2>> {log}'
        ' && exometrack=`basename "{input.exometrack}" | rev | cut -b 5- | rev` 2>> {log}'
        ' && configfile=$(mktemp) 2>> {log} 1>&2'
        ' && echo "C1 {input.healthy} `basename {input.healthy}`" > $configfile 2>> {log}'
        ' && echo "T1 {input.tumor} `basename {input.tumor}`" >> $configfile 2>> {log}'
        " && perl {wildcards.prefix}{config[dirs][intermediate]}{config[stepnames][excavator_install]}/{excavator_version}/EXCAVATORDataAnalysis.pl"
        " $configfile"
        " --processors {threads}"
        " --target $exometrack"
        " --assembly $assembly"
        " --output {output}"
        " --mode paired"
        " 2>> {log} 1>&2"
        # check if plot files have been generated, since it happened that above command exits with 0 although plot were not generated
        ' && find {output} -name "PlotResults*.pdf" | grep "PlotResults" >> {log}'


rule excavator_trio:
    input:
        assembly=lambda wildcards: "%s%s%s.fa" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(get_role(wildcards.trio.split('/')[0], wildcards.trio.split('/')[-1], 'patient', SAMPLESHEETS), SAMPLESHEETS, config)['file']),
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(get_role(wildcards.trio.split('/')[0], wildcards.trio.split('/')[-1], 'patient', SAMPLESHEETS), SAMPLESHEETS, config)),
        patient=directory(lambda wildcards: "%s%s%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'], get_role(wildcards.trio.split('/')[0], wildcards.trio.split('/')[-1], 'patient', SAMPLESHEETS))),
        father=directory(lambda wildcards: "%s%s%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'], get_role(wildcards.trio.split('/')[0], wildcards.trio.split('/')[-1], 'father', SAMPLESHEETS))),
        mother=directory(lambda wildcards: "%s%s%s/%s" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['excavator_dataprepare'], get_role(wildcards.trio.split('/')[0], wildcards.trio.split('/')[-1], 'mother', SAMPLESHEETS))),
    output:
        directory("{prefix}%s%s/{trio}" % (config['dirs']['intermediate'], config['stepnames']['excavator_trio']))
    log:
        "{prefix}%s%s/{trio}.log" % (config['dirs']['logs'], config['stepnames']['excavator_trio'])
    benchmark:
        "{prefix}%s%s/{trio}.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['excavator_trio'])
    conda:
        "envs/spike_excavator.yaml"
    threads:
        1
    params:
        name_trio=lambda wildcards: wildcards.trio.split('/')[-1]
    shell:
        # see excavator_install for the following line
        "rm -v -f $CONDA_PREFIX/etc/conda/activate.d/activate-r-base.sh 2> {log}"
        # make sure we are using conda's R and not the one loaded by module
        ' && module unload R 2>> {log}'
        ' && assembly=`basename "{input.assembly}" | rev | cut -b 4- | rev` 2>> {log}'
        ' && exometrack=`basename "{input.exometrack}" | rev | cut -b 5- | rev` 2>> {log}'
        ' && configfile=$(mktemp) 2>> {log} 1>&2'
        ' && echo "C1 {input.father} `basename {input.father}`" >> $configfile 2>> {log}'
        ' && echo "C2 {input.mother} `basename {input.mother}`" >> $configfile 2>> {log}'
        ' && echo "T1 {input.patient} `basename {input.patient}`" >> $configfile 2>> {log}'
        " && perl {wildcards.prefix}{config[dirs][intermediate]}{config[stepnames][excavator_install]}/{excavator_version}/EXCAVATORDataAnalysis.pl"
        " $configfile"
        " --processors {threads}"
        " --target $exometrack"
        " --assembly $assembly"
        " --output {output}"
        " --mode pooling"
        " 2>> {log} 1>&2"
        ' && find {output} -name "PlotResults*.pdf" | grep "PlotResults" >> {log}'
