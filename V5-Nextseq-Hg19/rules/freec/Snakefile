rule samtools_mpileup:
    # Preprocessing to do a realignment of the reads with GATK
    input:
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(wildcards.entity, SAMPLESHEETS, config)),
        references=lambda wildcards: ["%s%s%s%s" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(wildcards.entity, SAMPLESHEETS, config)['file'], ending) for ending in config['endings_bwa']],
        bam="{prefix}%s%s/{entity}.reCal.reAl.nodup.srt.bam" % (config['dirs']['intermediate'], config['stepnames']['gatk_PrintReads'])
    output:
        "{prefix}%s%s/{entity}.pileup.freec" % (config['dirs']['intermediate'], config['stepnames']['samtools_mpileup']),
    log:
        "{prefix}%s%s/{entity}.log" % (config['dirs']['logs'], config['stepnames']['samtools_mpileup'])
    benchmark:
        "{prefix}%s%s/{entity}./benchmark" % (config['dirs']['benchmarks'], config['stepnames']['samtools_mpileup'])
    conda:
        "envs/spike_freec.yaml"
    threads:
        1
    shell:
        "samtools"
        " mpileup"
        " -C50 "
        " -Q20"
        " -f {input.references[0]}"
        " -l {input.exometrack}"
        " {input.bam}"
        " 2> {log}"
        " > {output}"
        # " | pigz"
        # " 2> {log.pigz}"

        # /data/biotools/src/samtools-1.3/samtools mpileup -C50 -Q20 -f /data/biotools/reference/hGRC37/hGRC37.fa  -l $EXOME_TRACK $SAMPLE_BAM | pigz > $SAMPLE_PREFIX.pileup.gz & \
        # /data/biotools/src/samtools-1.3/samtools mpileup -C50 -Q20 -f /data/biotools/reference/hGRC37/hGRC37.fa  -l $EXOME_TRACK $CONTROL_BAM | pigz > $CONTROL_PREFIX.pileup.gz & \


rule freec:
    # Preprocessing to do a realignment of the reads with GATK
    input:
        tumor=lambda wildcards: "%s%s%s/%s.pileup.freec" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['samtools_mpileup'], get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'tumor', SAMPLESHEETS)),
        healthy=lambda wildcards: "%s%s%s/%s.pileup.freec" % (wildcards.prefix, config['dirs']['intermediate'], config['stepnames']['samtools_mpileup'], get_role(wildcards.entity.split('/')[0], wildcards.entity.split('/')[-1], 'healthy', SAMPLESHEETS)),
        exometrack=lambda wildcards: "%s%s%s" % (config['dirs']['prefix'], config['dirs']['references'], get_reference_exometrack(wildcards.entity, SAMPLESHEETS, config)),
        freec="%s%sfreec" % (config['dirs']['prefix'], config['dirs']['references']),
        reference_len=lambda wildcards: "%s%s%s" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(wildcards.entity, SAMPLESHEETS, config)['lengths']),
        reference_mappability=lambda wildcards: "%s%s%s" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(wildcards.entity, SAMPLESHEETS, config)['gemMappabilityFile']),
        reference_chromosomes=lambda wildcards: ["%s%s%s" % (wildcards.prefix, config['dirs']['references'], chr) for chr in get_reference_genome(wildcards.entity, SAMPLESHEETS, config)['chromosomes']],
        known_snps=lambda wildcards: "%s%s%s" % (wildcards.prefix, config['dirs']['references'], get_reference_genome(wildcards.entity, SAMPLESHEETS, config)['SNPfile']),
    output:
        "{prefix}%s%s/{entity}/tumor.pileup.freec_CNVs" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/tumor.pileup.freec_normal_BAF.txt" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/tumor.pileup.freec_normal_CNVs" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/tumor.pileup.freec_normal_ratio.txt" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/tumor.pileup.freec_ratio.txt" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        #"{prefix}%s%s/{entity}/tumor.pileup.freec_ratio.txt.log2.png" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        #"{prefix}%s%s/{entity}/tumor.pileup.freec_ratio.txt.png" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/tumor.pileup.freec_sample.cpn" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        "{prefix}%s%s/{entity}/healthy.pileup.freec_control.cpn" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        baf="{prefix}%s%s/{entity}/tumor.pileup.freec_BAF.txt" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        config="{prefix}%s%s/{entity}.config" % (config['dirs']['intermediate'], config['stepnames']['freec']),
    log:
        "{prefix}%s%s/{entity}.freec.log" % (config['dirs']['logs'], config['stepnames']['freec'])
    benchmark:
        "{prefix}%s%s/{entity}.freec.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['freec'])
    conda:
        "envs/spike_freec.yaml"
    threads:
        8
    params:
        minCNAlength=lambda wildcards: '"minCNAlength = 4"' if get_species(wildcards.entity, SAMPLESHEETS, config) == 'mus musculus' else ""
    shell:
        'echo "[general]" > {output.config} && '
        'echo "chrLenFile = {input.reference_len}" >> {output.config} && '
        'echo "window = 50000" >> {output.config} && '
        'echo "step = 10000" >> {output.config} && '
        'echo "#window = 500" >> {output.config} && '
        'echo "#step = 250" >> {output.config} && '
        'echo "ploidy = 2" >> {output.config} && '
        'echo "{params.minCNAlength}" >> {output.config} && '
        'echo "outputDir = $(dirname {output.baf})" >> {output.config} && '
        'echo "contaminationAdjustment = TRUE" >> {output.config} && '
        'echo "maxThreads = {threads}" >> {output.config} && '
        'echo "#sex=XY" >> {output.config} && '
        'echo "breakPointType=4" >> {output.config} && '
        'echo "gemMappabilityFile = {input.reference_mappability}" >> {output.config} && '
        'echo "chrFiles = $(dirname {input.reference_chromosomes[0]})/" >> {output.config} && '
        'echo "breakPointThreshold=1.5" >> {output.config} && '
        'echo "noisyData=TRUE" >> {output.config} && '
        'echo "printNA=FALSE" >> {output.config} && '
        'echo "" >> {output.config} && '
        'echo "[sample]" >> {output.config} && '
        'echo "mateFile = {input.tumor}" >> {output.config} && '
        'echo "inputFormat = pileup" >> {output.config} && '
        'echo "mateOrientation = 0" >> {output.config} && '
        'echo "" >> {output.config} && '
        'echo "[control]" >> {output.config} && '
        'echo "mateFile = {input.healthy}" >> {output.config} && '
        'echo "inputFormat = pileup" >> {output.config} && '
        'echo "mateOrientation = 0" >> {output.config} && '
        'echo "" >> {output.config} && '
        'echo "[BAF]" >> {output.config} && '
        'echo "SNPfile = {input.known_snps}" >> {output.config} && '
        'echo "# This one make the script crash" >> {output.config} && '
        'echo "#SNPfile = /data/biotools/reference/GRCm38.71_dbsnp/Mus_musculus.ref_clean.vcf.recode.vcf" >> {output.config} && '
        'echo "minimalCoveragePerPosition = 5" >> {output.config} && '
        'echo "" >> {output.config} && '
        'echo "[target]" >> {output.config} && '
        'echo "captureRegions = {input.exometrack}" >> {output.config} && '
        # create output dir
        ' mkdir -p $(dirname {output.baf}) &&'
        # actual command
        ' {input.freec}'
        ' -conf {output.config}'
        ' 2> {log}'
        ' 1>&2'
        ' && ls -la $(dirname {output.baf}) >> {log} 2>&1 '
        ' && mv $(dirname {output.baf})/*.pileup.freec_control.cpn $(dirname {output.baf})/healthy.pileup.freec_control.cpn 2>> {log}'
        ' && for ending in `echo "pileup.freec_BAF.txt pileup.freec_CNVs pileup.freec_normal_BAF.txt pileup.freec_normal_CNVs pileup.freec_normal_ratio.txt pileup.freec_ratio.txt pileup.freec_sample.cpn"`; do mv -v $(dirname {output.baf})/*.$ending $(dirname {output.baf})/tumor.$ending 2>> {log}; done'

rule freec_render:
    # this poses the issue of missing X11 server on HPC at HHU :-/
    # Do we really need those plots?
    input:
        "{prefix}%s%s/{tumorsample}/tumor.pileup.gz_ratio.txt" % (config['dirs']['intermediate'], config['stepnames']['freec'])
    output:
        linear="{prefix}%s%s/{tumorsample}/tumor.pileup.gz_ratio.txt.png" % (config['dirs']['intermediate'], config['stepnames']['freec']),
        log="{prefix}%s%s/{tumorsample}/tumor.pileup.gz_ratio.txt.log2.png" % (config['dirs']['intermediate'], config['stepnames']['freec'])
    log:
        "{prefix}%s%s/{tumorsample}.plot.log" % (config['dirs']['logs'], config['stepnames']['freec'])
    benchmark:
        "{prefix}%s%s/{tumorsample}.plot.benchmark" % (config['dirs']['benchmarks'], config['stepnames']['freec'])
    conda:
        "envs/spike_freec.yaml"
    threads:
        1
    shell:
        'cd $(dirname {input})'
        ' && cat $CONDA_PREFIX/bin/makeGraph.R'
        ' | R '
        ' --slave'
        ' --args 2'
        ' {input}'
        ' 2>> {log}'
        #' && for ending in `echo "pileup.gz_ratio.txt.log2.png pileup.gz_ratio.txt.png"`; do mv *.$ending tumor.$ending; done'

        # freec -conf $CONFIG_FILE && cd $OUTPUT_DIR && cat /data/biotools/src/freec_6.6/makeGraph.R | R --slave --args 2 $SAMPLE_PREFIX.pileup.gz_ratio.txt"
